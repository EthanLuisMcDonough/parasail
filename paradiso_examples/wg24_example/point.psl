interface Point<Flt is Float<>> is
  var X: Flt
  var Y: Flt

  func New(X: Flt; Y: Flt) -> Point

  func To_String(P: Point) -> Univ_String
  func From_String(Str: Univ_String) -> optional Point is (null)
  func Println(P: Point) is (Println(To_String(P)))
  op "=?"(P, Q: Point) -> Ordering
  func Hash(P: Point) -> Univ_Integer

  func Dist(P, Q: Point) -> Flt
  op "+" (P, Q: Point) -> Point
  op "+=" (var P: Point; Q: Point)
  op "-" (P, Q: Point) -> Point
  op "/" (P: Point; K: Flt) { K != 0.0 } -> Point
end interface Point

class Point is
    func Norm(P: Point) -> Flt is
      return Sqrt(P.X * P.X + P.Y * P.Y)
    end func Norm
  exports

    op "=?"(P, Q: Point) -> Ordering is
       if P.X != Q.X then
          return P.X =? Q.X
       else
          return P.Y =? Q.Y
       end if
    end op "=?"

    func New(X: Flt; Y: Flt) -> Point is
      return (X => X, Y => Y)
    end func New

    func To_String(P: Point) -> Univ_String is
      return "(" | P.X | ", " | P.Y | ")"
    end func To_String

    func Hash(P: Point) -> Univ_Integer is
      return Hash(104_543.0 * P.X + P.Y)
    end func Hash

    op "+" (P, Q: Point) -> Point is
      return (P.X + Q.X, P.Y + Q.Y)
    end op "+"

    op "+=" (var P: Point; Q: Point) is
      P.X += Q.X
      P.Y += Q.Y
    end op "+="

    op "-" (P, Q: Point) -> Point is
      return (P.X - Q.X, P.Y - Q.Y)
    end op "-"

    op "/" (P: Point; K: Flt) { K != 0.0 } -> Point is
      return (P.X / K, P.Y / K)
    end op "/"

    func Dist(P, Q: Point) -> Flt is
      return Norm(P - Q)
    end func Dist
end class Point
